###*
@namespace scoping into the hquery namespace
###
this.hQuery ||= {}


# =require core.coffee
###*
Observations generated by laboratories, imaging procedures, and other procedures. The scope
includes hematology, chemistry, serology, virology, toxicology, microbiology, plain x-ray,
ultrasound, CT, MRI, angiography, cardiac echo, nuclear medicine, pathology, and procedure
observations.
@class
@augments hQuery.CodedEntry
@exports Result as hQuery.Result
###
class hQuery.Result extends hQuery.CodedEntry
  constructor: (@json) ->
    super(@json)

  ###*
  ASTM CCR defines a restricted set of required result Type codes (see ResultTypeCode in section 7.3
  Summary of CCD value sets), used to categorize a result into one of several commonly accepted values
  (e.g. Hematology, Chemistry, Nuclear Medicine).
  @returns {CodedValue}
  ###
  resultType: -> this.type()

  ###*
  @returns {CodedValue}
  ###
  interpretation: -> hQuery.createCodedValue  @json['interpretation']

  ###*
  @returns {String}
  NOT used for QDM 5.0 referenceRange
  ###
  referenceRange: -> @json['referenceRange']

  ###*
  @returns {String}
  Used for QDM 5.0 referenceRange
  ###
  referenceRangeHigh: -> @json['referenceRangeHigh']

  ###*
  @returns {String}
  Used for QDM 5.0 referenceRange
  ###
  referenceRangeLow: -> @json['referenceRangeLow']

  ###*
  @returns {String}
  ###
  comment: -> @json['comment']

  ###*
  The resulting status of a procedure as defined in the QDM documentation. This is different
  than the status_code associated with the `CodedEntry` object, which relates to the data criteria
  status as defined in health-data-standards/lib/hqmf-model/data_criteria.json.
  @returns {CodedValue}
  ###
  qdmStatus: -> hQuery.createCodedValue  @json['qdm_status']

  ###*
  @returns {Date} The time the result report is generated and saved in the database.
  QDM 5.0 update for Laboratory Test, Performed
  ###
  resultDateTime: ->
    hQuery.dateFromUtcSeconds @json['result_date_time'] if @json['result_date_time']

  ###*
  @returns {Array, hquery.Component} an array of components
  ###
  components: ->  
    for  component in @json['Components']
      new hQuery.Component component
